default first_time_hospital = True
default first_time_sakura = True

label hospital:
    scene bg hospital with fade
    play music "audio/hospital_theme.mp3"

    if first_time_hospital:
        "Ты входишь в просторное помещение, оно выглядит довольно знакомо."
        "Прямо как в родном мире, стоят баночки, даже висит монитор, да и свет от ламп."
        "Только что-то отличается, но ты не можешь понять, что именно? Ты приглядываешься и замечаешь, что на телевизоре нет ни единой кнопки"
        "Но тут тебя от размышлений отрывает девушка с розовыми волосами, которая, похоже, здесь за главную."
        $ first_time_hospital = False
    else:
        "Ты снова в больнице, где всегда кипит работа."
    if first_time_sakura:
        show s neutral with dissolve
        s "О, новый пациент? Или ты просто интересуешься нашей работой?"
        s "Я Сакура, один из главных медиков города. Если тебе нужно лечение, обращайся."
        s "Но помни, лечение стоит денег."
        p "Деньги, лечение, это понятно, но что всё это вокруг? Как работает ваш телевизор, да и эти лампы?"
        s "Оу, а ты видимо не местный, дело в том, что наш ведущий маг считает здравоохранение - важнейшим."
        s "И всё вокруг является его личным изобретением, он уже давно покинул этот мир. Но его наследние, его забота обо всех стаждущих проявляется в этих кабинетах, на этих палатах."
        s "Раньше лечение было бесплатным, но теперь, когда величайшего мага с нами нет, ремонт и магические камни для этого оборудования стоят огромных денег, так что нам пришлось брать плату за лечение."
        p "То есть всё здесь работает на магических кристаллах? Никакого электричества?"
        s "Что такое \"э-ле-ктри-че-ство\"? Впервые слушу такое слово."
        p "Это-о, ну, как, хммм.. Ну как если бы энергия.. Ээээ, ладно, забудь"
        "Кажется ты не сможешь дать этому миру чудеса электричества, да, в том мире ты явно был не самым умным"
        s "Хорошо, странный мальчик, меня зовут [s.name], если тебе понадобится лечение - обращайся!"
        p "А меня зовут [hero_name], рад знакомству!"
    else:
        show s smile with dissolve
        s "Опять ты? Надеюсь, не сильно пострадал."
    jump hospital_sakura_menu

label hospital_sakura_menu:
    $ first_time_sakura = False
    menu:
        "Спросить, где можно найти главного медика" if not canVisit("surgency"):
            s "Ты имеешь в виду [ts.name]? Она лучшая лекарь в этом городе."
            s "Сейчас она в операционной, впрочем как и всегда. Если тебе нужно серьёзное лечение, направляйся туда."
            $ updateCanVisit("surgency", True)
            jump hospital_sakura_menu
        "Спросить про лечение" if health < 100:
            s "Поднять на ноги стоит 5 монет. Полное лечение от [ts.name] будет стоить дороже, но оно будет сильно эффективнее при сильезных ранениях. Если у тебя их нет, придётся найти способ заработать."
            if money >= 5:
                menu:
                    "Заплатить за лечение (5 монет)":
                        $minusMoney(5)
                        pause 3.5
                        $addHealth(20)
                        "Сакура осматривает тебя и использует медицинские техники."
                        "Ты чувствуешь, как боль исчезает, а силы возвращаются."
                    "Отказаться":
                        "Ты решаешь пока не тратить деньги на лечение."
            else:
                "У тебя нет даже 5 монет"
            jump hospital_sakura_menu
        "Перейти в операционную" if canVisit("surgency"):
            jump surgency_tsunade_cure
        "Спросить про задания" if isNoQuestNow():
            jump hospital_sakura_quests
        "Отказаться от выполнения задания" if isActualQuestOfCharacter("s"):
            p "Я не смогу выполнить это задание."
            s "Жаль, но потребность в травах есть всегда, если захочешь помочь - приходи."
            $ removeQuest()
            jump hospital_sakura_menu
        "Вернуться в город":
            jump city

label hospital_sakura_quests:
    show s smile with dissolve
    s "Ты хочешь помочь? Хорошо, у нас в больнице всегда есть работа."
    
    menu:
        "Задание на сбор трав":
            s @smile_closed_eyes "Хочешь помочь? Отлично! Вот список необходимых трав. Их можно найти в ближайшем лесу."
            if isAbleQuest(quest_sakura_materials, s_love):
                s @smile_shy "За хороший набор я неплохо заплачу."
                menu:
                    "Принять квест":
                        s @smile_shy "Будь осторожен, в лесу могут быть монстры."
                        $ getQuest(quest_sakura_materials)
                        jump hospital_sakura_menu
                    "Не принимать квест":
                        s @angry "А мог бы и помочь, теперь придется самой всё собирать."
                        jump hospital_sakura_menu
            else:
                s "Подожди, я вижу, ты пока не подходишь для этой работы, возвращайся, когда станешь сильнее."
                jump hospital_sakura_menu
        "Отказаться":
            s "Если передумаешь, только спроси"
            jump hospital_sakura_menu