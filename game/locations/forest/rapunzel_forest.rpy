label visit_rapunzel_forest:
    call forest_scene
    show r smile with dissolve

    if first_time_rapunzel and chosen_blessing.name == "Дар Соблазнителя":
        jump rapunzel_first_time_blessing
    elif first_time_rapunzel and chosen_blessing.name != "Дар Соблазнителя":
        jump first_time_rapunzel_no_blessing
    else:
        r "О, ты снова пришел?"
        jump rapunzel_forest

label rapunzel_first_time_blessing:
    $ first_time_rapunzel = False
    show r smile with dissolve
    r "О! Кто ты? Такой загадочный и... притягательный."

    "Она буквально пожирает меня взглядом. Её голубые глаза блестят любопытством, а в голосе звучит лёгкость. Ветер слегка колышет её длинные золотистые волосы."

    show r neutral with dissolve
    r "Я редко встречаю кого-то в этих местах... Ты выглядишь потерянным... или, может, ты просто ищешь приключений?"  
    r "Ммм… твоя аура… она… Что это?"  
    mind "Что этой красотке от меня надо?!"

    "Она делает осторожный шаг вперёд, а потом замирает, прикусывая губу. Её дыхание кажется чуть более тяжёлым, чем должно быть. Я чувствую, как в воздухе нарастает напряжение."

    show r smile with dissolve
    r "Это... странное ощущение. Мне хочется... подойти ближе."  

    "Ого. Это что, эффект благословения? Или она сама по себе такая открытая? Если так, мне стоит быть осторожнее… или воспользоваться моментом."  

    show r smirk with dissolve
    r "Нет! Так не пойдет! Мне нужно кое-что проверить!"

    p "Что? Проверить? О чем ты вообще?"  

    show r annoyed with dissolve
    r "Я должна знать... насколько ты хорош!"
    p "В чем? Мы даже не знакомы!"
    show r smirk with dissolve
    r "Ты знаешь, как правильно обращаться с женскими желаниями?"  
    mind "Кажется у меня есть шанс завалить ее в постель, подыграю ей"
    "Её язык коротко пробегает по нижней губе, и она смотрит на меня с вызывающей улыбкой. Весь её облик — словно вызов, приглашение в игру, где неясно, кто окажется победителем."

    menu:
        r "Ты знаешь, как правильно обращаться с женскими желаниями?"
        "Разумеется, я прирожденный мастер.":
            p "Разумеется, я прирожденный мастер."
            show r smile_shy with dissolve
            r "Хах, люблю уверенных мужчин. Но готов ли ты доказать это на деле?"

            "Она явно не собирается довольствоваться одними словами. В её глазах вспыхивает азарт, и, кажется, она уже решила, чем займётся со мной сегодня вечером."

        "Я просто действую по наитию.":
            p "Я просто действую по наитию."
            show r annoyed with dissolve
            r "О-о-о, интуиция – это интересно! Посмотрим, куда она тебя заведёт..."

            "Похоже, мой ответ её развеселил. Она кокетливо склонила голову набок, словно раздумывая, хочу ли я действительно играть с ней или просто тяну время."

        "А ты можешь меня научить?":
            p "А ты можешь меня научить?"
            show r smile with dissolve
            r "Хах! Неужели мне выпал шанс обучить самого обольстительного мужчину? Это даже забавно~"

            "Интересно, она шутит или действительно рассматривает меня как ученика? Хотя, зная таких девушек, обучение может быть весьма... увлекательным."

    show r smirk with dissolve
    r "Но перед этим... небольшая проверка!"  
    r "Как бы ты соблазнил девушку в таверне?"  

    mind "Проверка? Значит, это действительно игра. Ладно, посмотрим, насколько далеко она готова зайти."

    menu:
        r "Как бы ты соблазнил девушку в таверне?"
        "Подойти, взять её за руку и прошептать что-то возбуждающее на ухо.":
            p "Я бы подошел, взял её за руку и прошептал что-то возбуждающее на ухо."
            show r smile with dissolve
            r "О-о-о… звучит дерзко... Мне уже интересно, что будет дальше~"

            "Её пальцы слегка дёрнулись, а щёки окрасил лёгкий румянец. Кажется, она действительно представила этот момент."

        "Заказать ей выпивку и мило заговорить.":
            p "Заказать ей выпивку и мило заговорить."
            show r smile_shy with dissolve
            r "Классика. Надёжный вариант, но порой этого недостаточно."

            "Она хмыкнула, явно не впечатлённая. Ей нравятся более смелые шаги."

        "Просто поймать её взгляд и ждать, пока она сама подойдет.":
            p "Просто поймать её взгляд и ждать, пока она сама подойдет."
            show r smirk with dissolve
            r "Ммм… игры взглядов? Ты явно знаешь, как разогреть атмосферу."

            "Она смотрит на меня с новым интересом, словно решая, насколько я достоин её внимания."

    show r smirk with dissolve
    r "Знаешь, мне нравится твой стиль."  
    r "Ты завоевал её внимание. Что ты скажешь, когда она улыбнётся тебе?"

    menu:
        r "Что ты скажешь, когда она улыбнётся тебе?"
        "Ты выглядишь так, будто ждала именно меня.":
            p "Ты выглядишь так, будто ждала именно меня."
            show r smile_shy with dissolve
            r "О-о-о… Это прямое попадание. Даже я бы клюнула на такое~"

            mind "Её губы дрогнули, а ресницы чуть дрогнули. Кажется, мой ответ ей определённо понравился."

        "Знаешь, мне нужно твое мнение... какая поза лучше всего подходит для страстного поцелуя?":
            p "Знаешь, мне нужно твое мнение... какая поза лучше всего подходит для страстного поцелуя?"
            show r surprised with dissolve
            r "Хах! Оригинально! Теперь ты вынуждаешь меня представить это…"

            mind "Она явно застигнута врасплох. Я вижу, как её взгляд становится чуть мечтательным, и она невольно проводит пальцем по нижней губе."

        "Я могу предложить тебе вечер, который ты не забудешь.":
            p "Я могу предложить тебе вечер, который ты не забудешь."
            show r smile_shy with dissolve
            r "О-о-о, ты меня заинтриговал. Я даже не сомневаюсь, что ты сдержишь слово."

            mind "Она смотрит на меня с явным любопытством. Похоже, я точно пробудил в ней интерес."

    show r smirk with dissolve
    r "Ты мне нравишься... Но я хочу увидеть тебя в деле."  
    r "Пойдем со мной в одно особенное место. Ты должен доказать, что ты не просто болтаешь, а действуешь!"  

    mind "В её голосе звучит вызов. Она подаётся вперёд, так что её лицо оказывается почти вплотную к моему. Я чувствую запах её волос, лёгкий аромат трав и чего-то сладковатого. Если я пойду с ней, это определённо не закончится просто разговором."

    menu:
        r "Пойдем со мной!"
        "Согласиться и пойти с ней":
            p "Ладно, я заинтригован. Веди!"
            r closed_eyes "О, это будет весело~ Надеюсь, ты не разочаруешь меня..." with dissolve
            "Она берёт меня за руку — её ладонь тёплая и нежная. И, судя по её игривому взгляду, мы направляемся в место, которое точно перевернёт мой вечер."
        "Прикинуться непонимающим":
            p "О чем ты вообще говоришь?"
            show r smirk with dissolve
            r "Хах... Так-так, ты хочешь сыграть в невинного мальчика?"
            show r closed_eyes with dissolve
            r "Но я вижу, как ты смотришь на меня~"
            r "Ничего, я тебе всё покажу... и ты запомнишь этот день навсегда."
            "Она наклоняется ещё ближе, её губы почти касаются моего уха. Горячее дыхание обжигает кожу, а внутри меня разгорается любопытство... и не только оно."
    
    jump rapunzel_brothel_first_visit


label first_time_rapunzel_no_blessing:
    $ first_time_rapunzel = False
    show r neutral with dissolve
    r "Знаешь, я всегда была любопытной... Особенно когда встречаю кого-то необычного."

    "Её голос — тёплый, обволакивающий, словно мягкое прикосновение. Она пристально смотрит на меня, изучая, словно решает, стою ли я её внимания."  

    show r smirk with dissolve
    r "Ты не выглядишь, как великий обольститель... но, может, тебе просто не хватало практики?"  

    "Чувствую в её голосе лёгкую насмешку. Она хочет поддразнить меня, проверить, как я отреагирую. Что ж, мне определённо нравится её игривый настрой."  

    show r smile_shy with dissolve
    r "Давай проверим, насколько ты хорош в искусстве соблазна..."  

    "Она приближается на шаг, её губы слегка изогнуты в загадочной улыбке. Чёрт, она знает, как заставить мужчину почувствовать себя объектом внимания."  

    p "Ты хочешь, чтобы я попробовал тебя соблазнить?"  

    show r smile with dissolve
    r "О-о-о... Даже от одной мысли у меня пробежали мурашки по коже..."  

    "Хороший знак. Значит, я её заинтриговал."  

    show r smirk with dissolve
    r "Ну же, удиви меня! Давай сыграем... я буду твоей игрушкой~"  

    "Её голос стал тише, почти шёпотом, но в нём слышится вызов. Она ждёт, что я приму эту игру, что я проявлю себя... Интересно, насколько далеко она готова зайти?"  

    r "Допустим, мы встретились в таверне. Как ты подошёл бы ко мне?"  

    menu:
        r "Допустим, мы встретились в таверне. Как ты подошёл бы ко мне?"  
        "Я бы посмотрел тебе в глаза и сказал: 'Ты - самое красивое, что я видел сегодня'.":  
            p "Я бы посмотрел тебе в глаза и сказал: 'Ты - самое красивое, что я видел сегодня'."  
            r @closed_eyes "Ох… это мило… но тебе придется постараться сильнее." with dissolve
            "Её щёки слегка порозовели, но по глазам видно — этого недостаточно. Она явно ждет чего-то более смелого."  

        "Я бы подсел поближе и спросил, не хочешь ли ты приключений этим вечером.":  
            p "Я бы подсел поближе и спросил, не хочешь ли ты приключений этим вечером."    
            r @smirk "Хах, люблю мужчин, которые не тратят время на пустые разговоры~" with dissolve
            "Она одобрительно качает головой. Похоже, я попал в её вкусы."  

        "Я бы просто провел пальцами по твоему запястью, не говоря ни слова.":  
            p "Я бы просто провел пальцами по твоему запястью, не говоря ни слова."  
            r @surprised "О-о-о… Ты любишь играть на нервах, да?.. Интересный подход." with dissolve
            "Я замечаю, как её дыхание становится чуть глубже. Видимо, она не ожидала такого ответа, но он явно произвёл эффект."  

    show r smile_shy with dissolve
    r "Ты мне нравишься. Давай проверим, насколько ты хорош в деле~"

    "Она делает шаг назад, её улыбка остаётся загадочной. Мне кажется, или в её глазах сейчас блеснул настоящий азарт?"  

    "Что ж, если это испытание — я определённо не собираюсь его провалить."  

    call rapunzel_training
    jump rapunzel_forest


label rapunzel_forest: 
    show r smile with dissolve
    menu:
        "Посмотреть Бордель" if my_rapunzel.love >= 20 and not canVisit("brothel"):
            p "Я слышал, что где-то здесь есть бордель, ты случаем не знаешь о нем?"
            r @surprised "Только тихо, место это секретное. Идем." with dissolve
            jump rapunzel_brothel_first_visit
        "Попросить тренировку":
            r "Ах, ты хочешь улучшить свои навыки? Мне это нравится!"
            call rapunzel_training
            if last_charisma_training_win and first_r_root and my_rapunzel.love >= 50:
                $ first_r_root = False
                r @smile_shy "Ты так много трудишься и так много сделал для меня." with dissolve
                r @smirk "Может я могу помочь тебе расслабиться?" with dissolve
                menu:
                    r "Может я могу помочь тебе расслабиться?"
                    "Определенно можешь":
                        p "Ты точно можешь мне помочь!"
                        show r smile_shy with dissolve
                        r "..."
                        jump rapunzel_root_menu
                    "Ты? Чем?":
                        p "И чем ты можешь мне помочь? Какой мне толк от твоей помощи?"
                        r @neutral "уходи..." with dissolve
                        r annoyed "Я больше не хочу тебя видеть!" with dissolve
                        $can_go_r = False
                        "[r.name] силой выталкивает тебя из своей комнаты и запирает дверь на ключ"
                        $customNotify("Ты больше никогда не встретишь [r.name]")
                        $nextTime()
                        jump forest
            elif last_charisma_training_win:
                $addChar(["char"], 2)
                r closed_eyes "Хорошая работа, [hero_name]!" with dissolve
                $my_rapunzel.addLove(5)
            else:
                r annoyed "Мог бы хоть постараться!" with dissolve
                $minusLove ("r", 2)
            $nextTime()
            jump rapunzel_forest
        "Спросить про задание" if isNoQuestNow():
            p "У тебя не найдется задания для меня?"
            r "Хм... На самом деле, у меня есть кое-что интересное!"
            call rapunzel_quests
            jump rapunzel_forest
        "Отказаться от выполнения задания" if isActualQuestOfCharacter("r"):
            p "Я не смогу выполнить это задание."
            r "Ох, ну хорошо, если передумаешь - приходи."
            $ removeQuest()
            jump rapunzel_forest
        "Уйти":
            p "В другой раз, Рапунцель."
            show r annoyed with dissolve
            r "Ну вот, а я уже придумала, чем тебя занять~"
            r @smile "Ладно, не пропадай!" with dissolve
            jump forest

label rapunzel_quests: 
    menu:
        "Собрать особые грибы":
            r "Есть такие грибы... Они обладают уникальными свойствами."
            if isAbleQuest(quest_rapunzel_mashrooms, my_rapunzel.love):
                r "Если ты их найдешь, я буду просто в восторге!"
                $ getQuest(quest_rapunzel_mashrooms)
            else:
                r "Хотя нет, думаю ты пока не готов выполнить это задание."
        "Приглашать девушек подменить на работе":
            r "Нам нужно больше красивых и опытных девушек, но к сожалению работать некому."
            r "Если приведешь мне парочку, я тебя щедро отблагодарю~"
            $ getQuest(quest_rapunzel_women)
        "Я передумал":
            r "Ладно, но постарайся больше не отказывать девушкам в просьбах~~"
    return


label rapunzel_training:
    call start_charisma_training(r, charisma)
    return