define first_time_magic_tower = True

label magic_tower_elsa:
    if first_time_magic_tower:
        $ first_time_magic_tower = False
        jump magic_tower_first_visit
    else:
        jump magic_tower_return_with_elsa

label magic_tower_first_visit:
    scene magic_tower_exterior with fade

    "Передо мной возвышалась гигантская магическая башня."
    "Она выглядела не просто древней — её стены словно впитали в себя время."
    "Огромные каменные плиты покрывались тонкими линиями светящихся рун, медленно меняющих цвет. Казалось, башня дышит магией."

    "Чем ближе я подходил, тем сильнее ощущал странное покалывание в воздухе. Это место буквально бурлило энергией."

    show e neutral at center with dis5
    e "Не зевай. Идём внутрь."

    scene magic_tower_hall with fade

    "Мы вошли внутрь, и я на секунду потерял дар речи."

    "Величественный зал встречал нас высоким куполом, в центре которого парили тысячи крошечных светящихся сфер." 
    "Они плавно кружились, словно звёзды в миниатюре, освещая огромные книжные стеллажи, уходящие ввысь."

    "По стенам пробегали тонкие золотые линии магических узоров, образуя загадочные фигуры, а воздух был наполнен ароматом древних книг и чего-то едва уловимо сладкого... возможно, магии."

    "Я буквально ощущал, как энергия течёт сквозь эти стены."

    show mer confident at center with dis5
    "Но моё восхищение прервала женщина, стоявшая в центре зала. "
    "Высокая, с длинными чёрными волосами, холодным, но проницательным взглядом. "
    "Она скрестила руки на груди и с ухмылкой посмотрела на меня."

    show mer amused
    mer "[hero_name] выглядит так, будто впервые видит магию."

    show e smirk at right_mid with dis5
    e "Не просто впервые — он вообще понятия не имеет, что это такое."

    p "Эй! Я хотя бы слышал про магию!"

    show mer smirk
    mer "О, слышал? Какая редкость. Давай проверим, что ты знаешь."

    "Ух, мне не нравится этот её тон."

    ### ПЕРВАЯ ПРОВЕРКА: ОСНОВЫ МАГИИ  
    mer "Итак, первый вопрос. Что является основой любого магического заклинания?"

    menu:
        "Магическая палочка?":
            p "Магическая палочка!"
            show mer facepalm
            mer "О боги… Какой ужас. Нет."
        "Слова заклинания?":
            p "Наверное, слова заклинания?"
            show mer sigh
            mer "Я надеялась, что ты не настолько безнадёжен…"
        "Внутренняя магическая энергия?":
            p "Внутренняя магическая энергия!"
            show mer neutral
            mer "Хм… Верно, но я уверена, что ты сказал это наугад."

    "Эльза закатила глаза, явно не веря в мои способности."

    show e smirk
    e "Я предупреждала."

    ### ВТОРАЯ ПРОВЕРКА: РАЗЛИЧИЕ ЭЛЕМЕНТОВ  
    mer "Ладно, следующий вопрос. Чем отличается огненная магия от воздушной?"

    menu:
        "Огонь горячий, а воздух нет?":
            p "Ну... Огонь горячий, а воздух нет?"
            show mer annoyed
            mer "Гениально. Никогда бы не догадалась. Спасибо, что просветил меня."
        "Огонь сжигает, а воздух помогает дышать?":
            p "Огонь сжигает, а воздух помогает дышать?"
            show mer smirk
            mer "Ты определённо мастер очевидных истин."
        "Огонь уничтожает, а воздух может направлять магию?":
            p "Ну… Огонь уничтожает, а воздух может направлять магические потоки?"
            show mer surprised
            mer "Хм… Ты хоть что-то понимаешь. Может быть, из тебя ещё что-то выйдет."

    show e smirk
    e "Хотя бы одну догадку он угадал."

    ### ТРЕТЬЯ ПРОВЕРКА: ПРАКТИЧЕСКИЙ ЭКСПЕРИМЕНТ  
    mer "Последний тест. Покажи, что ты вообще можешь сделать с магией. Попробуй создать шар света."

    menu:
        "Попытаться сосредоточиться и представить свет":
            p "Я закрыл глаза и попытался представить шар света в своей ладони…"
            "..."
            "Ничего не произошло."
            show mer sigh
            mer "Ожидаемо."
        "Произнести какое-нибудь слово, как в книгах":
            p "Люмен! Люкс! Светоч! Дай мне свет!"
            "..."
            "Абсолютно ничего не произошло."
            show e amused
            e "Ахахах! Может, тебе попробовать 'абракадабра'?"
        "Потрясти руками, вдруг сработает":
            p "Я резко вытянул руки и начал трясти ими, пытаясь вызвать хоть какой-то эффект."
            "..."
            "Ничего."
            show mer neutral
            mer "Ты выглядел так глупо, что я даже не знаю, смеяться или плакать."

    show mer amused
    mer "Поздравляю, ты полностью провалил все три проверки."

    p "Ну, я хотя бы пытался!"

    show mer smirk
    mer "Да-да. Но в этот раз попытки не считаются. "
    mer "Хотя... Возможно, у тебя есть потенциал. "
    mer "Где-то глубоко-глубоко под слоем бесполезности."

    show e smirk
    e "Я знала, что это будет весело."

    show mer neutral
    mer "Ладно, раз уж Эльза тебя сюда привела, я дам тебе шанс. "
    mer "Добро пожаловать в магическую башню. Но учти: ты на самом дне. "
    mer "И тебе придётся очень постараться, чтобы хоть немного подняться."

    "Я не был уверен, что мне это нравится, но выбора у меня не было."

    $ can_visit_magic_tower = True
    jump magic_tower_hub

label magic_tower_return_with_elsa:
    scene magic_tower_hall with fade
    show e neutral

    "Я снова оказался у входа в магическую башню. Эльза шла рядом, не скрывая своего недовольства."

    p "Неужели я так плох?"

    e "Ты даже хуже, чем я ожидала. Но не волнуйся, у нас ещё есть время тебя исправить."

    show mer smirk
    mer "О, ты снова привела его? Ему не хватило унижения в прошлый раз?"

    p "Я слышу вас, если что..."

    mer "Вот и прекрасно. Значит, будешь слышать и мои уроки. Надеюсь, в этот раз ты не взорвёшь ничего лишнего."

    "Кажется, здесь меня ждёт очередной день мучений."

    jump magic_tower_hub

label magic_tower_alone:
    scene magic_tower_hall with fade

    "Я стоял перед величественной магической башней. В отличие от прошлого раза, теперь Эльзы рядом не было."

    "Я вздохнул и толкнул тяжёлую дверь, войдя внутрь."

    show mer neutral
    mer "Ну-ну, кто это у нас? И без няньки в этот раз."

    p "Я решил прийти сам."

    mer "Хм, и с чего бы такая решимость? Или Эльза просто выгнала тебя?"

    "Я не был уверен, стоит ли отвечать на это, но её пронизывающий взгляд давал понять, что лучше сказать правду."

    p "Я хочу научиться магии."

    show mer smirk
    mer "Посмотрим, насколько ты серьёзен."
    mer "Мне пора бежать, если понадоблюсь..."
    mer "Думаю ты сможешь меня найти."
    hide mer dis5

    jump magic_tower_hub

label magic_tower_hub:
    hide e
    hide mer
    "Я могу исследовать магическую башню, изучая магию и выполняя задания наставницы."
    
    menu:
        "Исследовать магическую башню":
            jump explore_magic_tower

        "Найти [mer.name]":
            jump find_merlin

        "Найти [e.name]":
            jump find_elsa

        "Вернуться в город":
            jump city

# Исследование магической башни с шансом найти "Великую книгу для Дураков"
label explore_magic_tower:
    scene magic_tower_library with fade

    "Я решил осмотреть магическую башню. "
    "Здесь было полно загадочных комнат, пыльных полок с книгами и странных артефактов."

    $ luck = renpy.random.randint(1, 100)

    if luck > 95:  # 5% шанс найти легендарную книгу
        "Пролистывая одну из книжных полок, я наткнулся на потрёпанную книгу с нелепым названием: 'Великая книга для Дураков'."

        "Я осторожно открыл её, и вдруг почувствовал, как мощный поток энергии пронёсся через меня!"

        $ mana += 10
        "Моя магическая энергия резко возросла! Кажется, я стал немного сильнее."
    
    else:
        "Я нашёл несколько старых свитков с записями о магии, но ничего особо ценного."
    jump magic_tower_hub

# Поиск Мерлин (шанс зависит от mana)
label find_merlin:
    scene magic_tower_hall with fade

    "Я отправился на поиски Мерлин. Она не любит, когда её беспокоят, но, возможно, мне повезёт."

    $ find_chance = mana * 5  # Чем выше mana, тем выше шанс
    $ roll = renpy.random.randint(1, 100)

    if roll <= find_chance:
        jump merlin_encounter
    else:
        "Я бродил по башне, но так и не смог её найти. Видимо, она ушла по своим магическим делам."
        "Интересно, что еще инетересного можно найти в башне"
        jump magic_tower_hub

label merlin_encounter:
    show mer smirk at center

    mer "Ты меня нашёл. Вопрос в том, зачем."

    menu:
        "Попросить урок":
            show mer amused
            mer "Ты действительно хочешь учиться? Ну что ж, посмотрим, как долго ты продержишься."
            "Мерлин начала объяснять сложные магические принципы, и мой мозг начал плавиться."
            $ mana += 2
            #TODO дописать, что она не может больше тратить на меня свое время и ей пора идти
            jump magic_tower_hub

        "Попросить квест":
            show mer neutral
            mer "Если хочешь задание – у меня всегда есть кое-что... рискованное."
            "Мерлин хитро улыбнулась и начала рассказывать про квест."
            #TODO дописать menu с квестами от Мерлин
            
        "Уйти":
            "Я решил не рисковать и оставить Мерлин в покое."
            jump magic_tower_hub

# Поиск Эльзы (всегда успешно) с меню взаимодействий
label find_elsa:
    scene magic_tower_training_room with fade
    show e neutral at center

    "Я нашёл Эльзу в тренировочном зале. "
    "Она как раз заканчивала очередное упражнение, заставляя ледяные копья исчезать в воздухе."

    show e smirk
    e "Решил проверить, насколько ты жалок сегодня?"
    jump find_elsa_menu

label find_elsa_menu:
    menu:
        "Потренироваться с ней":
            show e amused
            e "Хорошо. Только не плачь, если замёрзнешь."
            "Эльза начала тренировку, и я попытался повторять её движения... но быстро понял, что мне ещё далеко до её уровня."
            $ mana += 1
            jump find_elsa_menu
        "Попросить квест" if isNoQuestNow():
            show e neutral
            e "Квест? Есть пара дел, но предупреждаю – они тебе вряд ли понравятся."
            call elsa_quests_magic_tower
        "Уйти":
            "Я решил оставить Эльзу в покое и не мешать её тренировке."
            jump magic_tower_hub

label elsa_quests_magic_tower:
    menu:
        "Собирать магические материалы":
            p "Я готов собирать магические материалы."
            e "Хорошо, мне нужно несколько редких трав и минералов. Ты можешь найти их в этих лесах."
            $ active_quest = quest_elsa_materials
        "Стать испытуемым":
            p "Я готов стать подпытным кроликом."
            e "Я вижу, что ты довольно крепкий, если у тебя все еще хватает ума заходить в этот лес."
            e "Твоё тело мне пригодится~"
            $ active_quest = quest_elsa_test
            jump elsa_test
        "Я передумал":
            jump find_elsa_menu
    return